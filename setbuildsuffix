#!/bin/csh -f
if("$1" == "") then
	echo "Specify suffix"
	exit 1
endif
setenv BUILDSUFFIX "$1"
shift
setenv SDKCONFIG "sdkconfig"
if("$1" != "") setenv SDKCONFIG "$1"

if(! -e "$SDKCONFIG") then
	echo "Missing $SDKCONFIG"
	exit 0
endif

set add=()
set rem=()

if("$BUILDSUFFIX" =~ *-PICO*) then
	set add=($add CONFIG_ESP32_SPIRAM_SUPPORT CONFIG_ESPTOOLPY_FLASHSIZE_8MB)
	set rem=($rem CONFIG_ESPTOOLPY_FLASHSIZE_4MB CONFIG_FREERTOS_UNICORE)
else if("$BUILDSUFFIX" =~ *-SOLO*) then
	set add=($add CONFIG_ESPTOOLPY_FLASHSIZE_4MB CONFIG_FREERTOS_UNICORE)
	set rem=($rem CONFIG_ESP32_SPIRAM_SUPPORT CONFIG_ESPTOOLPY_FLASHSIZE_8MB)
else
	set add=($add CONFIG_ESPTOOLPY_FLASHSIZE_4MB)
	set rem=($rem CONFIG_ESP32_SPIRAM_SUPPORT CONFIG_ESPTOOLPY_FLASHSIZE_8MB CONFIG_FREERTOS_UNICORE)
endif

set bs=(`grep _BUILD_SUFFIX_ sdkconfig | sed 's/.*\(CONFIG_[A-Z0-9_]*_BUILD_SUFFIX_[A-Z0-9]*\).*/\1/'`)
foreach c ($bs)
	echo -n "$BUILDSUFFIX" | grep -q -- "-"`echo -n "$c" | sed 's/CONFIG_.*_BUILD_SUFFIX_\(.*\)/\1/'`
	if(! $status) then
		set add=($add $c)
	else
		set rem=($rem $c)
	endif
end

rm -f "$SDKCONFIG".new
foreach c ($add)
	grep -q "^# $c is not set" "$SDKCONFIG"
	if(! $status) then
		sed "s/^# $c is not set/$c=y/" "$SDKCONFIG" > "$SDKCONFIG.new"
		mv -f "$SDKCONFIG".new "$SDKCONFIG"
	endif
end
foreach c ($rem)
	grep -q "^$c=y" "$SDKCONFIG"
	if(! $status) then
		sed "s/^$c=y/# $c is not set/" "$SDKCONFIG" > "$SDKCONFIG.new"
		mv -f "$SDKCONFIG".new "$SDKCONFIG"
	endif
end


